from snakemake.utils import min_version


min_version("8.24")
shell.prefix("set -euo pipefail;")


# Constants loaded here.
include: "rules/common.smk"
include: "rules/utils.smk"


containerized: CONTAINER


# Main workflow.
module download_ref:
    snakefile:
        "rules/1-download_ref.smk"
    config:
        config


module concat_asm:
    snakefile:
        "rules/2-concat_asm.smk"
    config:
        config


module srf:
    snakefile:
        "rules/3-srf.smk"
    config:
        config


module align_asm_to_ref:
    snakefile:
        "rules/4-align_asm_to_ref.smk"
    config:
        config


module ident_cen_ctgs:
    snakefile:
        "rules/5-ident_cen_ctgs.smk"
    config:
        config


module repeatmasker:
    snakefile:
        "rules/6-repeatmasker.smk"
    config:
        config


module fix_cens_w_repeatmasker:
    snakefile:
        "rules/7-fix_cens_w_repeatmasker.smk"
    config:
        config


module humas_annot:
    snakefile:
        "rules/8-humas_annot.smk"
    config:
        config


module nucflag:
    snakefile:
        "rules/8-nucflag.smk"
    config:
        config


module cdr_finder:
    snakefile:
        "rules/8-cdr_finder.smk"
    config:
        config


module format_repeatmasker_sat_annot:
    snakefile:
        "rules/8-format_repeatmasker_sat_annot.smk"
    config:
        config


module get_complete_correct_cens:
    snakefile:
        "rules/9-get_complete_correct_cens.smk"
    config:
        config


module format_hor_stv:
    snakefile:
        "rules/10-format_hor_stv.smk"
    config:
        config


module plot_hor_stv:
    snakefile:
        "rules/11-plot_hor_stv.smk"
    config:
        config


module moddotplot:
    snakefile:
        "rules/11-moddotplot.smk"
    config:
        config


module calculate_hor_length:
    snakefile:
        "rules/11-calculate_hor_length.smk"
    config:
        config


module count_complete_cens:
    snakefile:
        "rules/12-count_complete_cens.smk"
    config:
        config


module plot_ideogram:
    snakefile:
        "rules/13-plot_ideogram.smk"
    config:
        config


use rule * from download_ref


use rule * from concat_asm


use rule * from srf


use rule * from ident_cen_ctgs


use rule * from repeatmasker


use rule * from fix_cens_w_repeatmasker


use rule * from format_repeatmasker_sat_annot


use rule * from get_complete_correct_cens


output = [
    # Required output.
    rules.srf_all.input,
    rules.repeatmasker_all.input,
    rules.fix_cens_w_repeatmasker_all.input,
    rules.get_complete_correct_cens_all.input,
]

if config["ident_cen_ctgs"].get("reference") or config.get("chromosomes"):

    use rule * from align_asm_to_ref


if config["ident_cen_ctgs"].get("reference") and config.get("chromosomes"):
    # Use reference and chromosomes. Don't download
    output.append(rules.ident_cen_ctgs_all.input)
elif not config["ident_cen_ctgs"].get("reference") and config.get("chromosomes"):
    # Chromosomes but no reference.
    # Download T2T-CHM13 reference assembly if existing path not provided.
    output.extend([rules.download_ref_asm.output, rules.ident_cen_ctgs_all.input])
else:
    # Otherwise don't remap contig names.
    print(
        "No reference or chromosomes. Not assigning chromosome name to assembly.",
        file=sys.stderr,
    )

if config.get("humas_annot"):

    use rule * from humas_annot

    use rule * from format_hor_stv

    output.append(rules.humas_annot_all.input)


# Omit assembly check. Primarily used for debugging faster.
if config.get("nucflag"):

    use rule * from nucflag

    output.append(rules.nucflag_all.input)


if config.get("humas_annot") and config.get("plot_hor_stv"):

    use rule * from plot_hor_stv

    output.extend(
        [
            rules.format_hor_stv_all.input,
            rules.format_repeatmasker_sat_all.input,
            rules.plot_hor_stv_all.input,
        ]
    )


if (
    config.get("humas_annot")
    and config.get("moddotplot")
    and config.get("plot_hor_stv")
):

    use rule * from moddotplot

    output.append(rules.moddotplot_all.input)


if config.get("humas_annot") and config.get("calculate_hor_length"):

    use rule * from calculate_hor_length

    use rule * from count_complete_cens

    output.extend(
        [
            rules.calculate_as_hor_length_all.input,
            rules.count_complete_cens_all.input,
        ]
    )


if config.get("ideogram"):

    use rule * from plot_ideogram

    output.append(rules.plot_ideogram_all.input)


if config.get("cdr_finder"):

    use rule * from cdr_finder

    output.append(rules.cdr_finder_all.input)


rule all:
    input:
        output,
    default_target: True
