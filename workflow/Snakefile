from snakemake.utils import min_version


min_version("6.0")
shell.prefix("set -euo pipefail;")


containerized: "docker://logsdonlab/cenmap:latest"


# Constants loaded here.
include: "rules/common.smk"


wildcard_constraints:
    chr="|".join(CHROMOSOMES),
    sm="|".join(SAMPLE_NAMES),
    hap="|".join(HAPLOTYPE),
    mer_order="|".join(MER_ORDER),


include: "rules/utils.smk"
include: "rules/download_ref.smk"
include: "rules/concat_asm.smk"
include: "rules/extract_ref_hor_arrays.smk"
include: "rules/align_asm_to_ref.smk"
include: "rules/ident_cen_ctgs.smk"
include: "rules/dna_brnn.smk"
include: "rules/extract_new_cens_ctgs.smk"
include: "rules/repeatmasker.smk"
include: "rules/fix_cens_w_repeatmasker.smk"
include: "rules/humas_hmmer.smk"


rule extract_cens_only:
    input:
        rules.extract_ref_hor_arrays_all.input,
        rules.concat_asm_all.input,
        rules.align_asm_ref_all.input,
        rules.ident_cen_ctgs_all.input,


output = [
    # Required output.
    rules.extract_cens_only.input,
    rules.dna_brnn_all.input,
    rules.extract_new_cens_all.input,
    rules.repeatmasker_only.input,
    rules.fix_cens_w_repeatmasker_only.input,
    rules.humas_hmmer_only.input,
]

# Download T2T-CHM13 reference assembly if existing path not provided.
if not config["align_asm_to_ref"].get("reference"):
    output.append(rules.download_ref_asm.output)

# Omit assembly check. Primarily used for debugging faster.
if config.get("nucflag"):

    include: "rules/nucflag.smk"

    output.append(rules.nucflag_only.input)


if config.get("plot_hor_stv"):

    include: "rules/plot_hor_stv.smk"
    include: "rules/plot_repeatmasker_sat_annot.smk"
    include: "rules/plot_cens_structure.smk"

    output.extend(
        [
            rules.plot_hor_stv_only.input,
            rules.plot_repeatmasker_sat_only.input,
            rules.plot_cens_structure_only.input,
        ]
    )


if config.get("moddotplot") and config.get("plot_hor_stv"):

    include: "rules/moddotplot.smk"

    output.append(rules.moddotplot_only.input)


if config.get("calculate_hor_length"):

    include: "rules/calculate_hor_length.smk"

    output.append(rules.get_hor_length_only.input)


if config.get("count_complete_cens"):

    include: "rules/count_complete_cens.smk"

    output.append(rules.count_complete_cens_only.input)


rule all:
    input:
        output,
    default_target: True
